defmodule Lux.Prisms.Telegram.Settings.PinChatMessage do
  @moduledoc """
  A prism to pin a message in a chat.
  Uses Telegram Bot API endpoint: POST /pinChatMessage
  """

  use Lux.Prism,
    name: "Pin Telegram Chat Message",
    description: "Pins a message in a chat or channel",
    input_schema: %{
      type: :object,
      properties: %{
        chat_id: %{
          type: [:string, :integer],
          description: "Unique identifier for the target chat or username of the target channel"
        },
        message_id: %{
          type: :integer,
          description: "Identifier of a message to pin"
        },
        disable_notification: %{
          type: :boolean,
          description: "Sends notification silently"
        }
      },
      required: ["chat_id", "message_id"]
    },
    output_schema: %{
      type: :object,
      properties: %{
        success: %{
          type: :boolean,
          description: "Whether the message was successfully pinned"
        }
      },
      required: ["success"]
    }

  alias Lux.Integrations.Telegram.Client
  require Logger

  def handler(params, agent) do
    with {:ok, chat_id} <- fetch_param(params, :chat_id),
         {:ok, message_id} <- fetch_param(params, :message_id) do
      
      agent_name = agent[:name] || "Unknown Agent"
      Logger.info("Agent #{agent_name} pinning message #{message_id} in #{chat_id}")

      request_body = %{
        chat_id: chat_id,
        message_id: message_id,
        disable_notification: fetch_optional(params, :disable_notification)
      }
      # Remove nil values
      request_body = Enum.filter(request_body, fn {_, v} -> not is_nil(v) end) |> Map.new()

      case Client.request(:post, "/pinChatMessage", %{json: request_body}) do
        {:ok, %{"ok" => true}} ->
          Logger.info("Successfully pinned message #{message_id} in chat #{chat_id}")
          {:ok, %{success: true}}

        {:error, {status, %{"description" => description}}} ->
          {:error, "Failed to pin message: #{description} (HTTP #{status})"}

        {:error, {status, description}} when is_binary(description) ->
          {:error, "Failed to pin message: #{description} (HTTP #{status})"}

        {:error, error} ->
          {:error, "Failed to pin message: #{inspect(error)}"}
      end
    end
  end

  defp fetch_param(params, key) do
    case Map.get(params, key) || Map.get(params, to_string(key)) do
      nil -> {:error, "Missing or invalid #{key}"}
      val -> {:ok, val}
    end
  end

  defp fetch_optional(params, key) do
    Map.get(params, key) || Map.get(params, to_string(key))
  end
end
